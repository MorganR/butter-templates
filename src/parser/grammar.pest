WHITESPACE = _{ SPACE_SEPARATOR | "\t" | NEWLINE }
comment_contents = { !NEWLINE ~ ANY }
COMMENT = _{ "//" ~ comment_contents* ~ NEWLINE }

package_name = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT)* ~ ("." ~ package_name)* }
package_statement = _{ "{" ~ "package" ~ package_name ~ "}" }

path_segment = @{ LETTER | NUMBER | CONNECTOR_PUNCTUATION | DASH_PUNCTUATION | SYMBOL | "." }
path_divider = _{ "/" }
path = @{ path_segment+ ~ (path_divider ~ path)? }

identifier = @{ XID_START ~ XID_CONTINUE* }
import_item = { identifier }
import_rename = { identifier }
import_wild = { "*" ~ "as" ~ import_rename }
import_items = { import_item ~ ("as" ~ import_rename)? ~ ("," ~ import_items)* }
import_selection = _{ import_wild | ( "{" ~ import_items ~ "}" ) }
import_source = @{ "'" ~ path ~ "'" }
import_statement = { "import" ~ import_selection ~ "from" ~ import_source } 

call = { identifier ~ "()" }
literal = { "lb" | "rb" }
inner_statement = { !block_statement_end ~ "{" ~ (call | literal) ~ "}"}

html_statement = { "html" ~ identifier }
html_character = _{ !("{" | "}") ~ ANY }
raw_html = { html_character+ }
html_content = _{ 
  ( inner_statement | raw_html ) ~ html_content?
}

block_statement_start = _{ "{" ~ html_statement ~ "}" }
block_content = { html_content }
block_statement_end = _{ "{/}" }
block_statement = {
  block_statement_start ~
  (block_statement | block_content) ~
  block_statement_end
}

main = {
  SOI ~
  package_statement ~

  import_statement* ~

  block_statement+ ~
  EOI
}